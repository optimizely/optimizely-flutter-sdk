plugins {
    id "com.android.library"
}

group 'com.optimizely.optimizely_flutter_sdk'
version '1.0'

def version_name = System.getenv('TRAVIS_TAG')
if (version_name != null) {
    rootProject.ext.version_name = version_name
} else {
    rootProject.ext.version_name = 'debugVersion'
}

configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}
rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}


ext {
    compile_sdk_version = 35
    min_sdk_version = 21
}

android {
    namespace 'com.optimizely.optimizely_flutter_sdk'
    compileSdkVersion rootProject.hasProperty('flutter.compileSdkVersion') 
                        ? rootProject.flutter.compileSdkVersion.toInteger() 
                        : compile_sdk_version

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        minSdkVersion min_sdk_version
        versionCode 1
        versionName version_name
        buildConfigField "String", "CLIENT_VERSION", "\"$version_name\""
        multiDexEnabled true

        // these rules will be merged to app's proguard rules
        consumerProguardFiles './proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'androidsupportmultidexversion.txt'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    //"logback-android" required for programmatic control of global sl4j log level.
    // - default log configuration in /assets/logback.xml
    // - [ref] https://github.com/tony19/logback-android
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.1.0"
    implementation "com.optimizely.ab:android-sdk:5.0.1"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation ('com.google.guava:guava:19.0') {
        exclude group:'com.google.guava', module:'listenablefuture'
    }
}
