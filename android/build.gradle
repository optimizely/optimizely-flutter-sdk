group 'com.optimizely.optimizely_flutter_sdk'
version '1.0'

buildscript {

    def version_name = System.getenv('TRAVIS_TAG')
    if (version_name != null) {
        rootProject.ext.version_name = version_name
    } else {
        rootProject.ext.version_name = 'debugVersion'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
    }
}
configurations {
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}
rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

ext {
    compile_sdk_version = 32
    build_tools_version = "30.0.3"
    min_sdk_version = 21
    target_sdk_version = 29
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName version_name
        buildConfigField "String", "CLIENT_VERSION", "\"$version_name\""
        multiDexEnabled true

        // these rules will be merged to app's proguard rules
        consumerProguardFiles './proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'androidsupportmultidexversion.txt'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}


dependencies {
    implementation 'androidx.multidex:multidex:2.0.0'
    implementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'

    //"logback-android" required for programmatic control of global sl4j log level.
    // - default log configuration in /assets/logback.xml
    // - [ref] https://github.com/tony19/logback-android
    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.6.10"
    implementation "com.optimizely.ab:android-sdk:4.0.0-beta2"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
    implementation ('com.google.guava:guava:19.0') {
        exclude group:'com.google.guava', module:'listenablefuture'
    }
}
